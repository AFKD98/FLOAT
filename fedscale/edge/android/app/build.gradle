buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    
    aaptOptions {
        noCompress "tflite"
    }

    compileSdkVersion 32
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.fedscale.android.app"
        minSdkVersion 23
        targetSdkVersion 32
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=TRUE", "-DANDROID_PLATFORM=android-21", "-DANDROID_STL=c++_shared"
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
//            main {
//                jniLibs.srcDirs = ['src/main/libs']
//            }
            assets {
                srcDirs = ["../assets"]
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/com.android.tools/proguard/coroutines.pro'
        pickFirst 'lib/armeabi-v7a/*'
        pickFirst 'lib/arm64-v8a/*'
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // App compat and UI things
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // You need to build grpc-java to obtain these libraries below.
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.grpc:grpc-okhttp:1.49.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf-lite:1.49.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.49.0' // CURRENT_GRPC_VERSION
    implementation 'net.razorvine:pickle:1.3'

    //WindowManager
    implementation 'androidx.window:window:1.1.0-alpha03'

    // Unit testing
    testImplementation 'junit:junit:4.13.2'

    // Instrumented testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Tensorflow lite dependencies
    implementation 'org.tensorflow:tensorflow-lite:2.9.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.9.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.2'
    implementation 'org.tensorflow:tensorflow-lite-select-tf-ops:2.9.0'
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.21.1' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.49.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}
